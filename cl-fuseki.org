cl-fuseki - Semantic Web Database Connection

#+tags: code lisp thesis rdf owl database semanticweb
#+PROPERTY: header-args :tangle no :cache no :session yes :results silent :no-expand no :noweb yes :exports code
#+startup: hideblocks overview

This file describes the a lisp-api for fuseki.

#+begin_src lisp :tangle cl-fuseki.lisp :exports none
  (in-package :cl-fuseki)
  
  <<support-functions>>
  
  <<simplifications>>

  <<postponed-updates>>
  
  <<drakma-configuration>>

  <<send-receive-request>>
  
  <<base-classes>>
  
  <<endpoint-construction>>
  
  <<logging>>

  <<query>>
  <<update>>
  <<ask>>
  <<insert-and-delete>>
#+end_src

#+RESULTS:
: #<package "CL-FUSEKI">

* Package description
cl-fuseki uses asdf for loading the library.  It depends on jsown, drakma and cl-ppcre, all available through quicklisp.

#+begin_src lisp :tangle cl-fuseki.asd
  (asdf:defsystem :cl-fuseki
    :name "cl-fuseki"
    :author "Aad Versteden <madnificent@gmail.com>"
    :version "0.0.1"
    :maintainer "Aad Versteden <madnificent@gmail.com>"
    :licence "MIT"
    :description "Library with basic support for Fuski through its web interface."
    :serial t
    :depends-on (drakma cl-ppcre jsown)
    :components ((:file "packages")
                 (:file "cl-fuseki")))
#+end_src

The package is exported to a separate file.  You should be able to #'use-package this in a lisp-environment containing only :cl

#+begin_src lisp :tangle packages.lisp
  (defpackage :cl-fuseki
    (:nicknames :fuseki)
    (:shadow :delete)
    (:use :cl :drakma :cl-ppcre :jsown)
    (:export :fuseki-server :fuseki-repository
             :name
             :query :long-query
             :update :long-update
             :ask
             :add-prefix :rm-prefix
             :insert :delete
             :*log-queries-p* :with-query-logging :without-query-logging))
  
  (defpackage :cl-fuseki-user
    (:nicknames :fuseki-user)
    (:shadowing-import-from :fuseki :delete)
    (:use :cl :cl-fuseki :jsown))
#+end_src

* Base classes
We denote the difference between a server and a repository, even though both may be very similar in construction.  Besides that we introduce the difference between a server and a fuseki-server, and between a repository and a fuseki-repository.  This may help in extending the library for related systems in the future.

The repository has a postponed-updates query, this is a cache which is explained in [[postponed updates]].

#+name: base-classes
#+begin_src lisp
  (defclass server ()
    ((base-url :accessor base-url
               :initarg :base-url
               :initform (error "base url must be supplied")))
    (:documentation "basic semantic web database server"))
  
  (defclass fuseki-server (server)
    ()
    (:documentation "fuseki semantic web database server"))
  
  (defclass virtuoso-server (server)
    ()
    (:documentation "Virtuoso sparql endpoint"))
  
  (defclass repository ()
    ((name :accessor name
           :initarg :name
           :initform "")
     (server :accessor server
             :initarg :server
             :initform (error "server must be supplied"))
     (postponed-updates :initform (make-hash-table :test 'equal))
     (unnamed-postponed-updates :initform nil))
    (:documentation "generic semantic web database repository"))
  
  (defclass fuseki-repository (repository)
    ()
    (:documentation "fuseki sementic web database repository"))
  
  (defclass virtuoso-repository (repository)
    ()
    (:documentation "virtuoso sparql endpoint repository"))
#+end_src

We will use these classes throughout the code.  They will supply extra requirements on certain code sections.

* Endpoint URLs
The query, update, data and upload sections may all connect to different connection points.  For Fuseki the fuseki-server objects will always be connected to fuseki-repository objects and vice-versa.

We publish a set of different endpoints for the server endpoints.  The server is responsible for publishing generic data, the repository is responsible for constructing a specific endpoint.

#+name: endpoint-construction
#+begin_src lisp
  (defgeneric query-endpoint (repository)
    (:documentation "SPARQL query endpoint"))
  (defgeneric update-endpoint (repository)
    (:documentation "SPARQL Update language endpoint"))
  (defgeneric data-endpoint (repository)
    (:documentation "SPARQL HTTP Update endpoint"))
  (defgeneric upload-endpoint (repository)
    (:documentation "file upload endpoint"))
  
  (defgeneric server-query-endpoint-postfix (server)
    (:documentation "postfix for the SPARQL query endpoint of the server"))
  (defgeneric server-update-endpoint-postfix (server)
    (:documentation "postfix for the SPARQL Update language endpoint of the server"))
  (defgeneric server-data-endpoint-postfix (server)
    (:documentation "postfix for the SPARQL HTTP Update endpoint of the server"))
  (defgeneric server-upload-endpoint-postfix (server)
    (:documentation "postfix for the file upload endpoint of the server"))
  
  (defmethod server-query-endpoint-postfix ((server fuseki-server))
    "/query")
  (defmethod server-update-endpoint-postfix ((server fuseki-server))
    "/update")
  (defmethod server-data-endpoint-postfix ((server fuseki-server))
    "/data")
  (defmethod server-upload-endpoint-postfix ((server fuseki-server))
    "/upload")
  
  (defmethod server-query-endpoint-postfix ((server virtuoso-server))
    "/sparql")
  (defmethod server-update-endpoint-postfix ((server virtuoso-server))
    "/sparql")
  (defmethod server-data-endpoint-postfix ((server virtuoso-server))
    "/sparql")
  (defmethod server-upload-endpoint-postfix ((server virtuoso-server))
    "/sparql")
  
  (defmethod query-endpoint ((repos fuseki-repository))
    (let ((server (server repos)))
      (s+ (base-url server) (name repos) (server-query-endpoint-postfix server))))
  (defmethod update-endpoint ((repos fuseki-repository))
    (let ((server (server repos)))
      (s+ (base-url server) (name repos) (server-update-endpoint-postfix server))))
  (defmethod data-endpoint ((repos fuseki-repository))
    (let ((server (server repos)))
      (s+ (base-url server) (name repos) (server-data-endpoint-postfix server))))
  (defmethod upload-endpoint ((repos fuseki-repository))
    (let ((server (server repos)))
      (s+ (base-url server) (name repos) (server-upload-endpoint-postfix server))))
  
  (defmethod query-endpoint ((repos repository))
    (let ((server (server repos)))
      (s+ (base-url server) (server-query-endpoint-postfix server))))
  (defmethod update-endpoint ((repos repository))
    (let ((server (server repos)))
      (s+ (base-url server) (server-update-endpoint-postfix server))))
  (defmethod data-endpoint ((repos repository))
    (let ((server (server repos)))
      (s+ (base-url server) (server-data-endpoint-postfix server))))
  (defmethod upload-endpoint ((repos repository))
    (let ((server (server repos)))
      (s+ (base-url server) (server-upload-endpoint-postfix server))))
#+end_src

* Query language
We implement the most basic query support.  There is no language which makes building SPARQL queries simpler, or which helps in parsing the results up to a certain level.

We support two query functions.  The first one is a generic query function which allows for the easy querying in one string.  There are unnamed optional keys which may alter the query.  These keys aren't necessarily stable, but are noted in [[simplification support]].

#+name: query
#+begin_src lisp
  (defgeneric query-raw (repository query &key &allow-other-keys)
    (:documentation "sends a raw sparql query to the repository.  this is meant to connect to the SPARQL query endpoint.  this version doesn't parse the result.
    see query for a version which returns a jsown parsed object of results"))
  
  (defgeneric query (repository query &key &allow-other-keys)
    (:documentation "sends a sparql query to the repository and returns a jsown-parsed object of results.  calls query-raw for the raw processing."))
  
  (defmethod query-raw ((repos repository) (query string) &rest options &key &allow-other-keys)
    (flush-updates repos)
    (let ((full-query (apply #'query-update-prefixes query options)))
      (maybe-log-query full-query)
      (send-request (query-endpoint repos)
                    :accept (get-data-type-binding :json)
                    :parameters `(("query" . ,full-query)))))
  
  (defmethod query-raw ((repos virtuoso-repository) (query string) &rest options &key &allow-other-keys)
    (flush-updates repos)
    (let ((full-query (apply #'query-update-prefixes query options)))
      (maybe-log-query full-query)
      (send-request (query-endpoint repos)
                    :method :post
                    :accept (get-data-type-binding :json)
                    :parameters `(("query" . ,full-query)))))
  
  (defmethod query ((repos repository) (query string) &rest options &key &allow-other-keys)
    (filter (parse (apply #'query-raw repos query options))
            "results" "bindings"))
  
  (defmacro long-query (repository (&rest options &key &allow-other-keys) &body query-forms) 
    "Provides a nicer visual for for executing a query which contains multiple lines."
    `(query ,repository
            (s+ ,@(loop for query in query-forms
                     append (list query " ")))
            ,@options))
#+end_src

* Update language
We implement the most basic update support and take the same approach as the one taken in [[query language]].  There is no language which makes building SPARQL updates simpler, or which helps in parsing the results up to a certain level.

The query language sported two query functions, providing different ways of receiving the parsed result.  As the update queries don't supply us with any meaningful information, we only support one version here.  There is an analogous function to long-query, named long-update.  There are unnamed optional keys which may alter the update.  These keys aren't necessarily stable, but are noted in [[simplification support]].

#+name: update
#+begin_src lisp
  (defgeneric update (repository query &key &allow-other-keys)
    (:documentation "sends a sparql update to the repository."))
  (defgeneric update-now (repository query)
    (:documentation "sends a sparql update query to the repository without waiting for anything"))

  (defmethod update-now ((repos repository) (update string))
    (maybe-log-query update)
    (send-request (update-endpoint repos)
                  :wanted-status-codes '(200 204) ; only 204 is in the spec
                  :content-type "application/sparql-update" ; fuseki-specific
                  :method :post
                  :content update))

  (defmethod update ((repos repository) (update string) &rest options &key &allow-other-keys)
    (apply #'maybe-postpone-update 
           repos
           update
           options))

  (defmacro long-update (repository (&rest options &key &allow-other-keys) &body query-forms) 
    "Provides a nicer visual for for executing an update query which contains multiple lines."
    `(query ,repository
            (s+ ,@(loop for query in query-forms
                     append (list query " ")))
            ,@options))
#+end_src

Next we provide an explicit interface for update and delete queries.  The requests in these functions use the standard header but take their other arguments in an equivalent way to the update method.

#+name: insert-and-delete
#+begin_src lisp
  (defmacro insert (repository (&rest options)
                    &body format)
    `(update ,repository
             (format nil "~&INSERT DATA~&{~A~&}"
                     (format nil ,@format))
             ,@options))
  
  (defmacro delete (repository (&rest options)
                    &body format)
    `(update ,repository
             (s+ "DELETE DATA { "
                 (format nil ,@format)
                 " }")
             ,@options))
#+end_src

* Ask language
The ask language is simple, it sends the given ASK query to the database and returns T if the database answered positively and NIL if the database answered negatively.

The implementation is analogous to the implementation of query, we reuse raw-query.

#+name: ask
#+begin_src lisp
  (defgeneric ask (repository query &key &allow-other-keys)
    (:documentation "sends a sparql ask query to the repository and returns T if the answer was positive or NIL if the ansewer was negative.  calls query-raw for the raw processing."))
  
  (defmethod ask ((repos repository) (query string) &rest options &key &allow-other-keys)
    (val (parse 
          (apply #'query-raw repos query options))
         "boolean"))
#+end_src

* Postponed updates
In some cases we may want to send updates only if a query occurs later on.  This can be so in cases where we're using the database as a reasoning entity.  For this reason we create the following two functions which will help in registering and querying postponed updates for the server.  As an added bonus, we introduce a variable which can be set to disable the postponed updates, as this can be handy for debugging or benchmarking.

# TODO check semantics of this postponed update
#      wrt database correctness
# (update-now repository 
#             (query-update-prefixes update-string))


#+name: postponed-updates
#+begin_src lisp
  (defparameter *do-postponed-updates* T)

  (defun maybe-postpone-update (repository update-string &rest options &key
                                (deposit nil depositp)
                                (revoke nil revokep) &allow-other-keys)
    "performs the update in a postponed fashion if deposit contains a key named deposit.  the update will be executed if a flush-updates function is called, or if a query is executed.  if another query with a revoke of a yet-to-be-executed update with a deposit-key that equals to that key is sent, then neither the query with the equaled deposit key as the query with the equaled revoke key will be executed."
    (if (or (not *do-postponed-updates*)
            (not (or depositp revokep)))
        (update-now repository
                    (apply #'query-update-prefixes
                           update-string
                           options))
        (if depositp
            (setf (gethash deposit
                           (slot-value repository
                                       'postponed-updates))
                  update-string)
            (unless (remhash revoke
                             (slot-value repository
                                         'postponed-updates))
              (push update-string
                    (slot-value repository
                                'unnamed-postponed-updates))))))

  (defun flush-updates (repository)
    "performs all postponed updates which still need to be executed"
    (let* ((hash (slot-value repository 'postponed-updates))
           (update-list (slot-value repository 'unnamed-postponed-updates))
           (keys (loop for key being the hash-keys of hash
                    collect key)))
      (when (or update-list keys)
        (update-now repository
                    (query-update-prefixes 
                     (format nil "~{~A~^; ~%~} ~[~;;~] ~{~A~^; ~%~}"
                             update-list
                             (or update-list keys)
                             (loop for key in keys collect (gethash key hash)))))
        (setf (slot-value repository 'unnamed-postponed-updates) nil)
        (dolist (key keys)
          (remhash key hash)))))
#+end_src

* Logging
Some use cases may benefit from logging the queries to be executed.  This makes debugging a lot simpler.  Logging should be optional, as it may hinder other usecases.

#+name: logging
#+begin_src lisp :exports none
  (defparameter *query-log-stream* nil
    "non-nil indicates that queries should be logged to the
     supplied stream.")
  
  (defun maybe-log-query (query)
    "Performs query-logging if *query-log-stream* is truethy."
    (when *query-log-stream*
      (format *query-log-stream*
              "~&==Executing query==~%~A~%~%"
              query))
    query)
  
  (defmacro with-query-logging (stream &body body)
    "Executes the following code block with query-logging enabled."
    `(let ((*query-log-stream* ,stream))
       ,@body))
  
  (defmacro without-query-logging (&body body)
    "Executes the following code-block with query logging disabled."
    `(let ((*query-log-stream* nil))
       ,@body))
#+end_src

* Simplification support
There are several patterns which keep occuring.  Whenever there's something that occurs too often, it's been added to this section.  Support for these things is optional and may change from time to time.

#+name: simplifications
#+begin_src lisp :exports none
  <<prefix-support>>
#+end_src

** Sparql prefix
Prefixes occur more often than not.  In order to minimize the amount of typing that needs to be done for these prefixes, we can add a standard set of prefixes to the sent queries/updates.  The query/update method may choose to ignore these prefixes.

#+name: prefix-support
#+begin_src lisp :exports none
<<prefix-support-prefix-variable>>
<<prefix-support-prefix-struct>>
<<prefix-support-prefix-p>>
<<prefix-support-public>>
<<prefix-support-implementation-public>>

; add standard prefixes
<<prefix-support-standard-prefixes>>
#+end_src

*** Public interface
We create two user-end functions, one to create the prefix and one to remove the prefix.  The interface is deliberately kept as simple as possible.

#+name: prefix-support-public
#+begin_src lisp
  (defun add-prefix (prefix iri)
    "Adds a prefix to the set of standard prefixes.  The prefix is the short version, the IRI is the long version.
     eg: (add-prefix \"rdf\" \"http://www.w3.org/1999/02/22-rdf-syntax-ns#\")"
    (when (is-standard-prefix-p prefix)
      (rm-prefix prefix))
    (push (make-prefix :prefix prefix :iri iri)
          *standard-prefixes*))

  (defun rm-prefix (prefix)
    "Removes a prefix from the set of standard prefixes.  The prefix is the short version.
     eg: (rm-prefix \"rdf\")"
    (when (is-standard-prefix-p prefix)
      (setf *standard-prefixes*
            (remove-if (lambda (prefix-prefix) (string= prefix prefix-prefix))
                       *standard-prefixes* :key #'prefix-prefix))))
#+end_src

There are some prefixes which occur all to often, we include them here by default.
#+name: prefix-support-standard-prefixes
#+begin_src lisp
  (add-prefix "rdf" "http://www.w3.org/1999/02/22-rdf-syntax-ns#")
  (add-prefix "owl" "http://www.w3.org/2002/07/owl#")
#+end_src

*** Query implementation interface
Towards queries, we provide one function.  It adds support for modifying a query with the necessary prefixes.  The function accepts an optional keyword which will remove the additions from the query.

#+name: prefix-support-implementation-public
#+begin_src lisp
  (defun query-update-prefixes (query &key (prefix T prefix-p) &allow-other-keys)
    "Updates the query unless the :prefix keyword has been set to nil."
    (if (or prefix (not prefix-p))
        (s+ (format nil "~{~&PREFIX ~A: <~A>~%~}"
                    (loop for p in *standard-prefixes*
                       append (list (prefix-prefix p) (prefix-iri p))))
            query)
        query))
#+end_src

*** Interal implementation
The internal interface consists of some helper functions, a struct and a special variable with a lisp list contained in it.

- prefix struct :: The prefix struct is used to easily store the prefixes.  It consists of the prefix and the iri and autimatically includes some handy helper functions.
                   #+name: prefix-support-prefix-struct
                   #+begin_src lisp
                     (defstruct prefix
                       (prefix)
                       (iri))
                   #+end_src
- special variable :: We create a special variable which contains all current standard prefixes.  These are the prefixes that can be added to a query.
     #+name: prefix-support-prefix-variable
     #+begin_src lisp
       (defvar *standard-prefixes* nil
         "contains all the standard prefixes, as prefix objects")
     #+end_src
     
- prefix inclusion check :: Some internal functions check whether or not a prefix is included in the current list of standard prefixes.
     #+name: prefix-support-prefix-p
     #+begin_src lisp
       (defun is-standard-prefix-p (prefix)
         "Checks whether or not the prefixed string is contained in the current list of standard prefixes.
          Returns non-nil if the prefix string is a known standard prefix."
         (find prefix *standard-prefixes* :key #'prefix-prefix :test #'string=))
     #+end_src

* Sending and receiving requests
For communicating with the server itself, we use drakma.  The following code allows us to send a request to the server and to parse the resulting triples.

#+name: send-receive-request
#+begin_src lisp
  (defun parse-ntriples-string (string)
    "converts an ntriples string into a list of triples (in which each triple is a list of three strings)"
    (mapcar (lambda (triple)
              (cl-ppcre:split "\\s+" triple))
            (cl-ppcre:split "\\s+\\.\\s+" string)))
  
  ;; data types
  (defparameter *data-type-bindings* (make-hash-table :test 'eq))
  
  (defun get-data-type-binding (symbol)
    (gethash symbol *data-type-bindings*))
  
  (defun (setf get-data-type-binding) (value symbol)
    (setf (gethash symbol *data-type-bindings*) value))
  
  (mapcar (lambda (k-v)
            (setf (get-data-type-binding (first k-v))
                  (second k-v)))
          '((:XML "application/sparql-results+xml")
            (:JSON "application/sparql-results+json")
            (:binary "application/x-binary-rdf-results-table")
            (:RDFXML "application/rdf+xml")
            (:NTriples "text/plain")
            (:Turtle "application/x-turtle")
            (:N3 "text/rdf+n3")
            (:TriX "application/trix")
            (:TriG "application/x-trig")
            (:PlainTextBoolean "text/boolean")))
  
  ;; errors
  (define-condition sesame-exception (error)
    ((status-code :reader status-code
                  :initarg :status-code)
     (response :reader response
               :initarg :response)))
  
  (defmacro remove-key (variable &rest keys)
    (let ((g-keys (gensym "keys")))
      `(let ((,g-keys (list ,@keys)))
         (setf ,variable (loop for (k v) on ,variable by #'cddr
                            unless (find k ,g-keys)
                            append (list k v))))))
  
  (defun send-request (url &rest html-args &key (wanted-status-codes '(200)) &allow-other-keys)
    (remove-key html-args :wanted-status-codes)
    (multiple-value-bind (response status-code)
        (apply #'http-request url html-args)
      (unless (and wanted-status-codes
                   (find status-code wanted-status-codes))
        (error 'sesame-exception
               :status-code status-code
               :response response))
      response))
#+end_src

We configure drakma so it assumes x-turtle and sparql-results+json are interpreted as text.  This ensures easy parsing of the response.  We alse ensuer that the requests and responses are assumed to be encoded in UTF8, rather than the default of latin1.  This increases compatibilty.


#+name: drakma-configuration
#+begin_src lisp
  ;; drakma setup
  (push (cons nil "x-turtle") drakma:*text-content-types*)
  (push (cons nil "sparql-results+json") drakma:*text-content-types*)
  
  (setf drakma:*drakma-default-external-format* :UTF-8)
#+end_src

* Support functions
This section contains various helper functions which haven't found their way into libraries just yet.

#+name: support-functions
#+begin_src lisp
  (defun s+ (&rest strings)
    "Concatenates a set of strings"
    (apply #'concatenate 'string "" strings))
#+end_src
